Class extends _interface

Class constructor($port : Integer; $huggingfaces : cs:C1710.event.huggingfaces; $options : Object; $event : cs:C1710.event.event)
	
	Super:C1705()
	
	var $ONNX : cs:C1710.workers.worker
	$ONNX:=cs:C1710.workers.worker.new(cs:C1710._server)
	
	If (Not:C34($ONNX.isRunning($port)))
		
		var $homeFolder : 4D:C1709.Folder
		$homeFolder:=Folder:C1567(fk home folder:K87:24).folder(".ONNX")
		
		If ($huggingfaces=Null:C1517) || (Not:C34(OB Instance of:C1731($huggingfaces; cs:C1710.event.huggingfaces))) || ($huggingfaces.huggingfaces.length=0)
			$folder:=$homeFolder.folder("Phi-3.5-mini-instruct")
			$URL:="microsoft/Phi-3.5-mini-instruct"
			$chat:=cs:C1710.event.huggingface.new($folder; $URL; "chat.completion")
			$folder:=$homeFolder.folder("all-MiniLM-L6-v2")
			$URL:="ONNX-models/all-MiniLM-L6-v2-ONNX"
			$embeddings:=cs:C1710.event.huggingface.new($folder; $URL; "embedding")
			$huggingfaces:=cs:C1710.event.huggingfaces.new([$chat; $embeddings])
		End if 
		
		If ($port=0) || ($port<0) || ($port>65535)
			$port:=8080
		End if 
		
		This:C1470._main($port; $huggingfaces; $options; $event)
		
	End if 
	
Function _main($port : Integer; $huggingfaces : cs:C1710.event.huggingfaces; $options : Object; $event : cs:C1710.event.event)
	
	main({name: Split string:C1554(Current method name:C684; "."; sk trim spaces:K86:2).first(); port: $port; huggingfaces: $huggingfaces; options: $options; event: $event}; This:C1470._onTCP)